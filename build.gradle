plugins {
	id 'java'
	id 'maven'
	id 'eclipse'
	id 'idea'
	id 'jacoco'
	id 'com.github.kt3k.coveralls' version '2.8.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'org.scm4j'
version = file('version').text.trim()

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

jacocoTestReport {
	reports {
		xml.enabled = true // coveralls plugin depends on xml format report
		html.enabled = true
	}
}

configurations.all {
	resolutionStrategy {
		cacheDynamicVersionsFor 0, 'seconds'
		cacheChangingModulesFor 0, 'seconds'
	}
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

repositories {
	mavenCentral()
	maven { 
		url "https://jitpack.io" 
	}
}

dependencies {                                                            
	// load managed dependencies
	def s = '[ \t]*', w = '[^:#@]+?'
	(file('mdeps').text =~ /(?m)^$s($w):($w):($w)?(:$w)?(@$w)?$s(?:#$s($w)?$s)?$/).each {
			match, group, name, version, classifier, ext, configuration ->
		add(configuration ?: name.contains('test') ? 'testCompile' : 'compile', "$group:$name:${version?:''}${classifier?:''}${ext?:''}")
	}
	compile 'org.eclipse.jgit:org.eclipse.jgit:4.3.0.201604071810-r'
	compile 'org.apache.commons:commons-lang3:3.5'
	compile 'commons-io:commons-io:2.6'
	
	testCompile 'org.mockito:mockito-core:2.0.62-beta'
	testCompile 'junit:junit:4.12'
}

jar {
	manifest.attributes (
			'Specification-Title': project.name,
			'Specification-Version': version,
			'Implementation-Title': project.name,
			'Implementation-Version': "$version (${new Date().format('yyyy-MM-dd')})",
	)
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}
	
task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}
